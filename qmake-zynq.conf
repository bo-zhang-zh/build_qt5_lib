#
# qmake configuration for linux-g++ using arm-xilinx-g++ compiler
#
 
MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib
 
include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)
 
load(device_config)
 
QT_QPA_DEFAULT_PLATFORM = linuxfb
 
# modifications to g++.conf
QMAKE_CC                = arm-linux-gnueabihf-gcc
QMAKE_CXX               = arm-linux-gnueabihf-g++
QMAKE_LINK              = arm-linux-gnueabihf-g++
QMAKE_LINK_SHLIB        = arm-linux-gnueabihf-g++
 
# modifications to linux.conf
QMAKE_AR                = arm-linux-gnueabihf-ar cqs
QMAKE_OBJCOPY           = arm-linux-gnueabihf-objcopy
QMAKE_NM                = arm-linux-gnueabihf-nm -P
QMAKE_STRIP             = arm-linux-gnueabihf-strip
 
QMAKE_CFLAGS           += -I$$[QT_SYSROOT]/include -DZYNQ
QMAKE_CXXFLAGS         += -Wno-psabi -I$$[QT_SYSROOT]/include -DZYNQ
QMAKE_LFLAGS           += -L$$[QT_SYSROOT]/lib
 
QMAKE_CFLAGS           += -march=armv7-a -mtune=cortex-a9 -pipe -fomit-frame-pointer
QMAKE_CXXFLAGS         += $$QMAKE_CFLAGS
 
load(qt_config)
